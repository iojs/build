pipeline {
    agent { label 'linter' }
    parameters{
        string(name: 'GITHUB_ORG', defaultValue: 'nodejs', description: 'The user/org of the GitHub repo')
        string(name: 'REPO_NAME', defaultValue: 'node', description: 'The name of the repo')
        string(name: 'GIT_REMOTE_REF', defaultValue: 'refs/heads/master', description: 'The remote portion of the Git refspec to fetch and test')
        string(name: 'REBASE_ONTO', defaultValue: '', description: 'Optionally, rebase onto the given ref before testing. Leave blank to skip rebasing.')
        string(name: 'POST_REBASE_SHA1_CHECK', defaultValue: '', description: 'After rebasing, check that the resulting commit sha1 matches the given one. If left blank, no check is performed.')
        string(name: 'CONFIG_FLAGS', defaultValue: '', description: 'Add arguments to ./configure.')
    }
    
    stages {
        stage("Setup repository") {
            steps {
                checkout(poll: false, scm: [
                    $class: 'GitSCM',
                    branches: [[
                        name: 'refs/heads/_jenkins_local_branch'
                    ]],
                    userRemoteConfigs: [[
                        credentialsId: "96d5f81c-e9ad-45f7-ba5d-bc8107c0ae2c",
                        url: "git@github.com:${params.GITHUB_ORG}/${params.REPO_NAME}",
                        refspec: "+${params.GIT_REMOTE_REF}:refs/remotes/origin/_jenkins_local_branch"
                    ]]
                ])
            }
        }
        
        stage('Pre-flight') {
            steps {
                sh "curl -L -s https://raw.githubusercontent.com/nodejs/build/master/jenkins/scripts/node-test-commit-pre.sh -s | bash -xe"
                sendBuildStatus("pending", env)
                checkMake()
                sh 'node --version'
            }
        }
        
        stage('Build linting tools') {
            steps {
                // this target might not exist 
                sh(returnStatus: true, script: "${env.MAKE} lint-md-build")
            }
        }
         
        stage('Run tests') {
            steps {
                sh "NODE=node ${env.MAKE} lint-ci"
            }
        }
    }
    
    post {
        always {
            // Until we fix the TAP generator
            // tap2JUnit()
        }

        success {
            sendBuildStatus("success", env)
        }

        failure {
            // If lint-ci fails, print all the interesting lines to the console.
            sh("cat test-eslint.tap | grep -v '^ok\\|^TAP version 13\\|^1\\.\\.|^' | awk 'NF'")
            sendBuildStatus("failure", env)
        }
    }
}

def tap2JUnit() {
    fileOperations([folderCreateOperation('out/junit')])
    def status = sh(returnStatus: true, script: 'tap2junit -i test-eslint.tap -o out/junit/test-eslint.xml')
    if (status == 0) {
        junit(allowEmptyResults: true, testResults: 'out/junit/*.xml')
    }
}

def checkMake() {
    def status = sh(returnStatus: true, script: "which gmake")
    if (status != 0) {
        env.MAKE = 'make'
    } else {
        env.MAKE = 'gmake'
    }
}

def sendBuildStatus(status, env) {
    return
    build job: 'post-build-status-update', parameters: [
              string(name: 'REPO', value: 'node'),
              string(name: 'IDENTIFIER', value: 'node-test-linter'),
              string(name: 'URL', value: env.BUILD_URL),
              string(name: 'COMMIT', value: sh(script: 'git rev-parse HEAD', returnStdout: true).trim()),
              string(name: 'REF', value: env.GIT_REMOTE_REF),
              string(name: 'STATUS', value: status)
          ]
}
