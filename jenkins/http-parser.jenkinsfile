#!/usr/bin/env groovy

import groovy.json.JsonOutput

pipeline {
    agent { label 'jenkins-workspace' }

    parameters {
        booleanParam(name: 'CERTIFY_SAFE', defaultValue: false, description: 'I have reviewed *the latest version of* these changes and I am sure that they donâ€™t contain any code that could compromise the security of the CI infrastructure.')
        string(defaultValue: "", description: 'The numeric ID of the pull request (from GitHub URL)', name: 'PR_ID')
        string(defaultValue: 'master', description: 'Git branch to use for testing (ignore this value for PRs)', name: 'BRANCH')
    }

    stages {
        stage("Setup repository") {
            steps {
                checkout(poll: false, scm: [
                    $class: 'GitSCM',
                    branches: [[
                        name: (params.PR_ID ? "pr/${params.PR_ID}/head" : params.BRANCH)
                    ]],
                    userRemoteConfigs: [[
                        url: "https://github.com/nodejs/http-parser",
                        refspec: '+refs/pull/*:refs/remotes/origin/pr/* +refs/heads/*:refs/remotes/origin/*',
                    ]]
                ])
            }
        }

        stage('Setup build dependencies') {
            steps {
                echo "Testing: ${sh(returnStdout: true, script: 'git log -1 --oneline').trim()}"

                sh 'gcc --version'
                sendBuildStatus("pending", env.PR_ID, env.BUILD_URL)
            }
        }

        stage('Run tests') {
            steps {
                sh 'make'
            }
        }
    }

    post {
        success {
            sendBuildStatus("success", env.PR_ID, env.BUILD_URL)
        }

        failure {
            sendBuildStatus("failure", env.PR_ID, env.BUILD_URL)
        }
    }
}

def sendBuildStatus(status, prId, buildUrl) {
    if (!prId) { return }

    def path = ""
    def message = ""

    if (status == "success") {
        message = "tests passed"
        path = "end"
    } else if (status == "failure") {
        message = "tests failed"
        path = "end"
    } else if (status == "pending") {
        message = "checking for errors"
        path = "start"
    }

    def buildPayload = JsonOutput.toJson([
        'identifier': 'test',
        'status': status,
        'message': message,
        'commit': sh(returnStdout: true, script: 'git rev-parse HEAD').trim(),
        'url': buildUrl,
        'ref': "refs/pull/${env.PR_ID}/head"
        ])

    sh(returnStdout: true, script: "curl -H 'Content-Type: application/json' -X POST -d '${buildPayload}' http://9d7d96c6.ngrok.io/http-parser/jenkins/${path}")
}
